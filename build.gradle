import java.util.concurrent.TimeUnit

apply plugin: 'java-library'

static String envOr(String key, String presentPrefix = "", Object or = "") {
    def env = System.getenv()
    if (env.containsKey(key))
        return presentPrefix + env.get(key)
    return or
}

ext {
    buildId = TimeUnit.MILLISECONDS.toMinutes(System.currentTimeMillis())
}
task tcBuildId {
    doLast {
        println "##teamcity[buildNumber '$buildId']"
    }
}
tasks.named('jar').configure { dependsOn tcBuildId }
group 'org.comroid'
version '1.1' + '.' + buildId

apply from: 'gradle/vars.gradle'

sourceCompatibility = 17
targetCompatibility = 17

tasks.findByName('wrapper')?.configure {
    gradleVersion = '6.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

configurations {
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

tasks.withType(Javadoc) {
    source = sourceSets.main.java

    options {
        encoding = 'UTF-8'
        /*
        links = [
                "https://docs.oracle.com/javase/8/docs/api/",
                "https://docs.oracle.com/javaee/7/api/"
        ]
        */
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://www.jitpack.io' }
    mavenCentral()
}

dependencies {
    // annotations
    api 'org.jetbrains:annotations:+'
    compileOnly 'com.fasterxml.jackson.core:jackson-annotations:2.+'

    compileOnly 'org.slf4j:slf4j-api:1.+'

    // lombok
    api 'org.projectlombok:lombok:+'
    annotationProcessor 'org.projectlombok:lombok:+'

    testImplementation 'junit:junit:+'
    testImplementation 'org.easymock:easymock:+'
}

apply from: 'gradle/publishing.gradle'
